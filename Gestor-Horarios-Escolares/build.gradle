import org.gradle.internal.os.OperatingSystem

plugins {
    id 'java'
    id 'application'
    id 'org.openjfx.javafxplugin' version '0.0.10'
    id 'org.beryx.jlink' version '2.24.1'
}

group 'com.gamma'
version '1.0.0'
description 'Sistema gestor de horarios escolares'

repositories {
    mavenCentral()
}

ext {
    junitVersion = '5.8.1'
    calendarfxVersion = '11.12.3'
    bootstrapfxVersion = '0.4.0'
    mysqlConnectorVersion = '8.0.30'
    sql2oVersion = '1.6.0'
    jlinkCompression = '2'
}

sourceCompatibility = '15'
targetCompatibility = '15'

tasks.withType(JavaCompile) {
    options.encoding = 'UTF-8'
}

application {
    mainModule = 'com.gamma.gestorhorariosescolares'
    mainClass = 'com.gamma.gestorhorariosescolares.App'
}

javafx {
    version = '15.0.1'
    modules = ['javafx.controls', 'javafx.fxml']
}

dependencies {

    testImplementation("org.junit.jupiter:junit-jupiter-api:${junitVersion}")
    testRuntimeOnly("org.junit.jupiter:junit-jupiter-engine:${junitVersion}")
    //implementation("com.calendarfx:view:${calendarfxVersion}")
    implementation("org.kordamp.bootstrapfx:bootstrapfx-core:${bootstrapfxVersion}")
    implementation("mysql:mysql-connector-java:${mysqlConnectorVersion}")
    implementation("org.sql2o:sql2o:${sql2oVersion}")
}

test {
    useJUnitPlatform()
}

jlink {
    imageZip = project.file("${buildDir}/distributions/app-${javafx.platform.classifier}.zip")
    options = ['--strip-debug', '--compress', project.ext.jlinkCompression, '--no-header-files', '--no-man-pages']
    launcher {
        name = project.name
    }
    jpackage {
        installerOptions = [
                '--description', description,
                '--copyright', 'Copyrigth 2022-2022 GAMMA',
                '--vendor', 'Equipo GAMMA'
        ]
        if (OperatingSystem.current().windows) {
            installerOptions += ['--win-per-user-install', '--win-dir-chooser', '--win-menu', '--win-shortcut']
            imageOptions += ['--icon', "src/main/resources/com/gamma/gestorhorariosescolares/compartido/infrestructura/imagenes/icon.ico"]
        }
    }
}

jlinkZip {
    group = 'distribution'
}

jpackage {
    doFirst {
        //https://bugs.openjdk.java.net/browse/JDK-8254920
        if (OperatingSystem.current().windows && project.ext.jlinkCompression == '2') {
            project.copy {
                from "${buildDir.getAbsolutePath()}/${name}/${project.name}/runtime/bin/zip.dll"
                into "${buildDir.getAbsolutePath()}/${name}/${project.name}"
            }
        }
    }
}