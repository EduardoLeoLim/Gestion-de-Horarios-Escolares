plugins {
    id 'java'
    id 'application'
    id 'org.openjfx.javafxplugin' version '0.0.10'
    id 'org.beryx.jlink' version '2.24.1'
}

group 'com.gamma'
version '1.0.0'

repositories {
    mavenCentral()
}

ext {
    junitVersion = '5.8.1'
    description("Gestor de horarios escolares")
}

sourceCompatibility = '15'
targetCompatibility = '15'

tasks.withType(JavaCompile) {
    options.encoding = 'UTF-8'
}

application {
    mainModule = 'com.gamma.gestorhorariosescolares'
    mainClass = 'com.gamma.gestorhorariosescolares.App'
}

javafx {
    version = '15.0.1'
    modules = ['javafx.controls', 'javafx.fxml']
}

dependencies {

    testImplementation("org.junit.jupiter:junit-jupiter-api:${junitVersion}")
    testRuntimeOnly("org.junit.jupiter:junit-jupiter-engine:${junitVersion}")
    implementation("org.kordamp.bootstrapfx:bootstrapfx-core:0.4.0")
    implementation("mysql:mysql-connector-java:8.0.30")
    implementation("org.sql2o:sql2o:1.6.0")
}

test {
    useJUnitPlatform()
}

jlink {
    imageZip = project.file("${buildDir}/distributions/app-${javafx.platform.classifier}.zip")
    options = ['--strip-debug', '--compress', '2', '--no-header-files', '--no-man-pages']
    launcher {
        name = 'Gestor Escolar'
    }
    jpackage {
        installerOptions = [
                '--description', description,
                '--copyright', 'Copyrigth 2022-2022 GAMMA'
        ]
        installerType = project.findProperty('installerType')
        if (installerType == 'msi') {
            imageOptions += ['--icon', "src/main/resources/com/gamma/gestorhorariosescolares/compartido/infrestructura/imagenes/icon.ico"]
            installerOptions += [
                    '--win-per-user-install', '--win-dir-chooser',
                    '--win-menu', '--win-shortcut'
            ]
        }
    }
}

jlinkZip {
    group = 'distribution'
}

jpackage {
    doFirst {
        project.getProperty('installerType') // throws exception if its missing
    }
}